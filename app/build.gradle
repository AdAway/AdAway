plugins {
    id 'com.android.application'
}

def keyStoreDefined = project.hasProperty('signingStoreLocation') &&
        project.hasProperty('signingStorePassword') &&
        project.hasProperty('signingKeyAlias') &&
        project.hasProperty('signingKeyPassword')

repositories {
    maven {
        url 'https://jitpack.io'
    }
}

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'
    ndkVersion '23.1.7779620'

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 51000   // Read x.yy.zz to match version name
        versionName '5.10.0'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    /*
     * To sign release build, create file gradle.properties in ~/.gradle/ with this content:
     *
     * signingStoreLocation=/home/key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    if (keyStoreDefined) {
        println "Found signature properties in gradle.properties. Build will be signed."

        signingConfigs {
            release {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }

        buildTypes.debug.signingConfig = signingConfigs.release
        buildTypes.release.signingConfig = signingConfigs.release
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    buildTypes {
//        debug {
//            shrinkResources false
//            minifyEnabled false
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Do not abort build if lint finds errors
    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    // Native modules
    implementation project(':tcpdump')
    implementation project(':webserver')

    // AndroidX components
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.fragment:fragment:1.4.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.4.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.paging:paging-runtime:3.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.room:room-runtime:2.3.0'
    annotationProcessor 'androidx.room:room-compiler:2.3.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime:2.7.1'
    implementation 'com.google.android.material:material:1.4.0'

    // Collections related
    implementation 'com.google.guava:guava:31.0.1-android'
    // Network related
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    // Logging related
    implementation 'com.jakewharton.timber:timber:5.0.1'
    if (keyStoreDefined) {
        implementation project(':sentrystub')
    } else {
        implementation 'io.sentry:sentry-android:3.1.0'
    }

    // Root related
    implementation 'com.github.topjohnwu.libsu:core:3.2.1'
    implementation 'com.github.topjohnwu.libsu:io:3.2.1'

    // VPN related
    implementation 'org.pcap4j:pcap4j-core:1.8.2'
    implementation 'org.pcap4j:pcap4j-packetfactory-static:1.8.2'
    implementation 'dnsjava:dnsjava:3.4.3'
    implementation 'org.slf4j:slf4j-android:1.7.32'

    // Test related
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.json:json:20201115'
    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'junit:junit:4.13.2'
}
