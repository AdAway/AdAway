apply plugin: 'com.android.application'

def keyStoreDefined = project.hasProperty('signingStoreLocation') &&
        project.hasProperty('signingStorePassword') &&
        project.hasProperty('signingKeyAlias') &&
        project.hasProperty('signingKeyPassword')

repositories {
    maven {
        url 'https://jitpack.io'
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    ndkVersion '22.0.7026061'

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 50300   // Read x.yy.zz to match version name
        versionName '5.3.0'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    /*
     * To sign release build, create file gradle.properties in ~/.gradle/ with this content:
     *
     * signingStoreLocation=/home/key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    if (keyStoreDefined) {
        println "Found signature properties in gradle.properties. Build will be signed."

        signingConfigs {
            release {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }

        buildTypes.debug.signingConfig = signingConfigs.release
        buildTypes.release.signingConfig = signingConfigs.release
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding = true
    }

    buildTypes {
//        debug {
//            shrinkResources false
//            minifyEnabled false
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Do not abort build if lint finds errors
    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    // Native modules
    implementation project(':tcpdump')
    implementation project(':webserver')

    // AndroidX components
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.fragment:fragment:1.2.5'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.paging:paging-runtime:2.1.2'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.room:room-runtime:2.2.6'
    annotationProcessor 'androidx.room:room-compiler:2.2.6'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime:2.5.0'
    implementation 'com.google.android.material:material:1.3.0'

    // Miscellaneous
    implementation 'com.google.guava:guava:30.1-android'
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'org.sufficientlysecure:html-textview:3.9'
    if (keyStoreDefined) {
        implementation project(':sentrystub')
    } else {
        implementation 'io.sentry:sentry-android:3.1.0'
    }

    // Root related
    implementation 'com.github.topjohnwu.libsu:core:2.5.1'
    implementation 'com.github.topjohnwu.libsu:io:2.5.1'

    // VPN related
    implementation 'org.pcap4j:pcap4j-core:1.8.2'
    implementation 'org.pcap4j:pcap4j-packetfactory-static:1.8.2'
    implementation 'dnsjava:dnsjava:3.3.1'
    implementation 'org.slf4j:slf4j-android:1.7.30'

    // Test related
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.json:json:20201115'
    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'junit:junit:4.13.1'
}
