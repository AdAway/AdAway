apply plugin: 'com.android.application'

def keyStoreDefined = project.hasProperty('signingStoreLocation') &&
        project.hasProperty('signingStorePassword') &&
        project.hasProperty('signingKeyAlias') &&
        project.hasProperty('signingKeyPassword')

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 40205   // Read 4.xx.yy to match version name
        versionName '4.2.5'

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    /*
     * To sign release build, create file gradle.properties in ~/.gradle/ with this content:
     *
     * signingStoreLocation=/home/key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    if (keyStoreDefined) {
        println "Found signature properties in gradle.properties. Build will be signed."

        signingConfigs {
            release {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }

        buildTypes.debug.signingConfig = signingConfigs.release
        buildTypes.release.signingConfig = signingConfigs.release
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Do not abort build if lint finds errors
    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext {
    appcompat = '1.1.0-alpha2'
    constraint_layout = '1.1.3'
    lifecycle = '2.0.0'
    material = '1.1.0-alpha04'
    room = '2.0.0'
    support = '1.0.0'
    work = '1.0.0'
}

dependencies {
    implementation project(':libraries:RootCommands')
    implementation project(':tcpdump')
    implementation project(':webserver')

    // Architecture components
    implementation "androidx.lifecycle:lifecycle-extensions:${lifecycle}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${lifecycle}"
    implementation "androidx.room:room-runtime:${room}"
    annotationProcessor "androidx.room:room-compiler:${room}"
    implementation "android.arch.work:work-runtime:${work}"

    // Support libraries
    implementation "androidx.appcompat:appcompat:1.1.0-alpha03"
    implementation "androidx.cardview:cardview:${support}"
    implementation "androidx.constraintlayout:constraintlayout:${constraint_layout}"
    implementation "androidx.preference:preference:${support}"
    implementation "androidx.recyclerview:recyclerview:1.1.0-alpha03"
    implementation "androidx.fragment:fragment:1.1.0-alpha4"
    implementation "com.google.android.material:material:${material}"

    // Miscellaneous
    api 'com.google.guava:guava:27.0.1-android'
    implementation 'com.annimon:stream:1.2.1'
    implementation 'com.google.android:flexbox:1.1.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'net.sf.trove4j:trove4j:3.0.3'
    implementation 'org.sufficientlysecure:html-textview:3.4'
    if (keyStoreDefined) {
        implementation project(':sentrystub')
    } else {
        implementation 'io.sentry:sentry-android:1.7.14'
    }
    implementation 'org.slf4j:slf4j-nop:1.7.25'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20180130'
}
