plugins {
    id 'com.android.application'
}

boolean keyStoreDefined = project.hasProperty('signingStoreLocation') &&
        project.hasProperty('signingStorePassword') &&
        project.hasProperty('signingKeyAlias') &&
        project.hasProperty('signingKeyPassword')

repositories {
    maven {
        url 'https://jitpack.io'
    }
}

android {
    compileSdk 36
    namespace 'org.adaway'

    defaultConfig {
        minSdk 26
        targetSdk 36
        versionCode libs.versions.appCode.get() as int
        versionName libs.versions.appName.get()

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    /*
     * To sign release build, create file gradle.properties in ~/.gradle/ with this content:
     *
     * signingStoreLocation=/home/key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    if (keyStoreDefined) {
        println "Found signature properties in gradle.properties. Build will be signed."

        signingConfigs {
            release {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }

        buildTypes.debug.signingConfig = signingConfigs.release
        buildTypes.release.signingConfig = signingConfigs.release
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    buildTypes {
//        debug {
//            shrinkResources false
//            minifyEnabled false
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Do not abort build if lint finds errors
    lint {
        disable 'MissingTranslation'
    }
}

dependencies {
    // Native modules
    implementation project(':tcpdump')
    implementation project(':webserver')

    // AndroidX components
    implementation libs.androidx.appcompat
    implementation libs.androidx.cardview
    implementation libs.androidx.constraintlayout
    implementation libs.fragment
    // "fragment-ktx" is not used but was added to fix the following dependency error:
    // Duplicate class androidx.lifecycle.ViewModelLazy found in modules lifecycle-viewmodel-2.5.0-runtime (androidx.lifecycle:lifecycle-viewmodel:2.5.0) and lifecycle-viewmodel-ktx-2.3.1-runtime
    implementation libs.androidx.fragment.ktx
    implementation libs.androidx.localbroadcastmanager
    implementation libs.androidx.paging.runtime
    implementation libs.androidx.preference
    implementation libs.androidx.recyclerview
    implementation libs.androidx.room.runtime
    implementation libs.androidx.room.paging
    annotationProcessor libs.androidx.room.compiler
    implementation libs.androidx.swiperefreshlayout
    implementation libs.androidx.work.runtime
    implementation libs.material

    // Collections related
    implementation libs.guava
    // Network related
    implementation libs.okhttp3.okhttp
    // Logging related
    implementation libs.timber
    if (keyStoreDefined) {
        implementation project(':sentrystub')
    } else {
        implementation platform('io.sentry:sentry-bom:7.8.0')
        implementation('io.sentry:sentry-android')
        implementation('io.sentry:sentry-android-fragment')
        implementation('io.sentry:sentry-android-timber')
    }

    // Root related
    implementation libs.libsu

    // VPN related
    implementation libs.bundles.pcap4j
    implementation libs.dnsjava
    implementation libs.slf4j.android
    implementation libs.okhttp.dnsoverhttps

    // Test related
    testImplementation libs.junit
    testImplementation libs.json
    androidTestImplementation libs.bundles.androidx.test
    androidTestImplementation libs.junit
}
