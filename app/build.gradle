apply plugin: 'com.android.application'

def getGitSha1 = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 40008   // Read 4.xx.yy to match version name
        versionName '4.0.8'
        versionNameSuffix '-' + getGitSha1()
		
		vectorDrawables.useSupportLibrary = true
        
		javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    /*
     * To sign release build, create file gradle.properties in ~/.gradle/ with this content:
     *
     * signingStoreLocation=/home/key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    if (project.hasProperty('signingStoreLocation') &&
            project.hasProperty('signingStorePassword') &&
            project.hasProperty('signingKeyAlias') &&
            project.hasProperty('signingKeyPassword')) {
        println "Found signature properties in gradle.properties. Build will be signed."

        signingConfigs {
            release {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }

        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

	// Do not abort build if lint finds errors
    lintOptions {
        abortOnError false
    }
}

ext {
	lifecycle = '1.1.1'
	room = '1.1.1'
	supportLib = '28.0.0-rc01'
}

dependencies {
	implementation project(':libraries:RootCommands')
    implementation project(':tcpdump')
    implementation project(':webserver')

    // Architecture components
    implementation "android.arch.lifecycle:extensions:${lifecycle}"
    implementation "android.arch.lifecycle:common-java8:${lifecycle}"
    implementation "android.arch.persistence.room:runtime:${room}"
    annotationProcessor "android.arch.persistence.room:compiler:${room}"
    implementation 'android.arch.work:work-runtime:1.0.0-alpha08'

    // Support libraries
    implementation "com.android.support:design:${supportLib}"
    implementation "com.android.support:support-v4:${supportLib}"
    implementation "com.android.support:appcompat-v7:${supportLib}"
    implementation "com.android.support:cardview-v7:${supportLib}"
    implementation "com.android.support:preference-v7:${supportLib}"
    implementation "com.android.support:recyclerview-v7:${supportLib}"
    implementation "com.android.support:support-fragment:${supportLib}"
	implementation 'com.android.support.constraint:constraint-layout:1.1.3'

	// Miscellaneous
    implementation 'com.annimon:stream:1.2.1'
	implementation 'net.sf.trove4j:trove4j:3.0.3'
    implementation 'org.sufficientlysecure:html-textview:3.4'
}
